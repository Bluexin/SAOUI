buildscript {
    ext.kotlin_version = '1.0.5'
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "maven"
apply plugin: 'kotlin'
apply plugin: 'forge'

version = "1.7.10-2.0-lite-SNAPSHOT"
group = "com.saomc" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "saoui"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.7.10-10.13.4.1614-1.7.10"
    runDir = "run"
    mappings = "stable_12"
}

repositories {
    mavenCentral()
    maven {
        url = "https://maven.bluexin.be/repository/releases/"
    }
}

minecraft {
    srgExtra "PK: gnu/jel saoui/shade/gnu/jel"
    srgExtra "PK: kotlin be/bluexin/saomclib/shade/kotlin"
    srgExtra "PK: org/jetbrains/annotations be/bluexin/saomclib/shade/annotations"
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile("be.bluexin:saomc-lib:1.7.10-1.0.5b:deobf")
    shade files('lib/jel-2_1_1/lib/jel_g_homebaked.jar')
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea { module { inheritOutputDirs = true } }

uploadArchives {
    if (System.hasProperty('REPO_PWD') != null) {
        repositories.mavenDeployer {
            repository(url: "http://maven.bluexin.be/repository/" + (((String) project.version).contains("SNAPSHOT")? "snapshots": "releases") + "/") {
                authentication(userName: "CI", password: System.getenv('REPO_PWD'))
            }
        }
    } else {
        println("No password set up! Uploading to local.")
        repositories.mavenDeployer {
            repository(url: 'file://G:/saoui/build/mvn')
        }
    }
}

task sourceJar(type: Jar, dependsOn: "sourceMainJava", overwrite: true) {
    from "build/sources/main/java"
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives sourceJar
    archives deobfJar
}

jar {
    for (dep in configurations.shade) {
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

task apiArtifact(type: Jar, dependsOn: compileJava) {
    from sourceSets.main.output
    include "com/saomc/saoui/api/**"
    classifier = 'api'
}

tasks.withType(Copy) {
    includeEmptyDirs = false
}
